* WTF?

Learning [[https://github.com/digego/extempore][Extempore]] while following [[https://www.lightnote.co/course/][LightNote]] music theory course.

* Setup

  To run examples you need Extempore's master branch HEAD compiled. Version 0.7
  doesn't fit, because Extempore API is undergoing substantial change. Some of
  code suppose knowledge obtained from [[http://digego.github.io/extempore/index.html][official documentation]], especially about
  setup and language basics.

  To follow the course you need access to [[https://app.lightnote.co/][app,]] could be purchased [[https://www.lightnote.co/course/?ref=sidebarpremium#buy][here.]] But
  following course is not required for reading this document, especially if you
  are already familiar with basic music theory and came here for Extempore
  examples.

  If you are proficient with org-mode, you already know how it would best for
  you to run examples. Otherwise you have two basic options:

  1. Copy and paste code to buffer/editor from which you know how to send it to
     Extempore compiler (see [[http://digego.github.io/extempore/index.html][documentation]]). Blocks are enclosed with xml-like
     comments to help you because GitHub org renderer doesn't do tangling. HTML
     exported version is included in repo (read it [[http://ul.mantike.pro/lightnote/][here]]) for easier following,
     but it's not guaranteed to be up-to-date.
  2. If you have Emacs installed then run =tangle.sh= to produce xtm files and
     run code from them. Xml-like comments with block names helps here with
     following too. Generated files are included in this repo either, but they
     are not guaranteed to be up-to-date.

* The Essential Guide to Music Theory

** Sound
*** [[https://app.lightnote.co/sound][Sound]]

    To produce sound in Extempore we need to setup xtlang callback:

#+NAME: set-dsp
#+BEGIN_SRC extempore
  ;; <set-dsp>
  (dsp:set! dsp)
  ;; </set-dsp>
#+END_SRC

#+NAME: xtm/00-sound-silence.xtm
#+BEGIN_SRC extempore :tangle xtm/00-sound-silence.xtm :noweb yes :mkdirp yes :padline no
  ;; <xtm/00-sound-silence.xtm>
  (bind-func dsp:DSP
    (lambda (in time chan dat)
      0.0))
  <<set-dsp>>
  ;; </xtm/00-sound-silence.xtm>
#+END_SRC

    Note callback signature:

    - in:SAMPLE :: sample from input device
    - time:i64 :: sample number
    - chan:i64 :: audio channel
    - dat:SAMPLE* :: user data
    - <return>:SAMPLE :: sample at given channel and time

    sample value varies from -1.0 to 1.0

    You can set dsp function once, but then redefine it as many times as your
    want. Our first attempt produces silence, let's make it more audible:

#+NAME: xtm/01-sound-sine.xtm
#+BEGIN_SRC extempore :tangle xtm/01-sound-sine.xtm :noweb yes :mkdirp yes :padline no
  ;; <xtm/01-sound-sine.xtm>
  (bind-func dsp:DSP
    (lambda (in time chan dat)
      (let ((amplitude 0.5)
            (frequency 440.0))
        (* amplitude
           (sin (* frequency
                   (/ STWOPI SRf)
                   (convert time)))))))
  <<set-dsp>>
  ;; </xtm/01-sound-sine.xtm>
#+END_SRC

    =STWOPI= is /2pi of type SAMPLE/ constant, and =convert= allows us to make a
    =SAMPLE= typed value from =time=. =SRf= refers to current sampling frequency.
    Extempore uses symbiosis of two different languages with similar, LISPy,
    syntax: Scheme and xtlang. Performance-sensitive parts (usually dsp) are
    implemented in xtlang, and other stuff (usually control) is done in Scheme.
    xtlang is very much like C but with LISP syntax and proper closures.

    So far so good, we've obtained a basic form of sound â€” a sine wave.
    Amplitude, or height of the wave (in case you are following graphics in
    course), in our example is half of maximum available. =sin= ranges from -1.0
    to 1.0 and we multiply it by 0.5. It affects sound loudness. Try to play with
    it.

    Frequency, or density of the wave, is perceived as a pitch. Play with it.

**** MIDI controller

     While the essence of live coding is performance created with code,
     cyber-physical environment incorporates various media. Let's plug MIDI
     controller and play with amplitude and frequency using it. For that purpose
     we are going to load =midi_input= library:

#+NAME: load-midi-input
#+BEGIN_SRC extempore
  ;; <load-midi-input>
  (sys:load "libs/external/midi_input.xtm")
  ;; </load-midi-input>
#+END_SRC

     It load a =portmidi= wrapper and tries to connect to the first midi device.
     The latter fact is important because if you will try to connect to this
     device again by =(set_midi_in 0)= you will get unhelpful error message
     /Invalid device ID./

     Look into console where you are running Extempore. =midi_input= calls
     =(pm_print_devices)= on startup. If you MIDI controller is listed under the
     index 0 then nothing to do. Otherwise execute (replace *3* with required index):

#+NAME: set-midi-in
#+BEGIN_SRC extempore
  ;; <set-midi-in>
  (set_midi_in 3)
  ;; </set-midi-in>
#+END_SRC

     To make our =dsp= function controllable outside let's move =amplitude= and
     =controller= outside of lambda:

#+NAME: sine-closure-dsp
#+BEGIN_SRC extempore
  ;; <sine-closure-dsp>
  (bind-func dsp:DSP
    (let ((amplitude 0.5)
          (frequency 440.0))
      (lambda (in time chan dat)
          (* amplitude
             (sin (* frequency
                     (/ STWOPI SRf)
                     (convert time)))))))
  ;; </sine-closure-dsp>
#+END_SRC

     xtlang has a nice feature: closure environment is accessible outside using
     dot-syntax, =(closure.variable:type)= as getter and =(closure.variable:type
     value)= as setter. This feature is arguable from the point of view of
     functional style leaning towards purity and referential transparency, but I
     guess it provides good trade for performance.

     To read values from controller we would override =midi_cc= function callback
     provided by =midi_input= (replace *19* and *23* with your knobs CCs):

#+NAME: sine-midi-cc
#+BEGIN_SRC extempore
  ;; <sine-midi-cc>
  (bind-func midi_cc
    (lambda (timestamp:i32 controller:i32 value:i32 chan:i32)
      (println "MIDI CC" controller value)
      (cond ((= controller 19) (dsp.amplitude:SAMPLE (/ (convert value) 127.)))
            ((= controller 23) (dsp.frequency:SAMPLE (* (convert value) 10.)))
            (else 0.0:f))
      void))
  ;; </sine-midi-cc>
#+END_SRC

     If you execute snippets one-by-one then you should have response already.
     Otherwise here is entire file:

#+NAME: xtm/02-sound-sine-midi.xtm
#+BEGIN_SRC extempore :tangle xtm/02-sound-sine-midi.xtm :noweb yes :mkdirp yes :padline no
  ;; <xtm/02-sound-sine-midi.xtm>
  <<load-midi-input>>
  <<sine-closure-dsp>>
  <<set-dsp>>
  ;; <<set-midi-in>>
  <<sine-midi-cc>>
  ;; </xtm/02-sound-sine-midi.xtm>
#+END_SRC

*** [[https://app.lightnote.co/harmony][Harmony]]

    This section involves playing notes, to ease tinkering with them let's
    introduce instruments. Extempore instrument is essentially a pair of
    functions which knows how to render note of the given frequency and
    amplitude. Let's call our first intrument just a =tuner=, because it doesn't
    care about shape of the note of any sound effects, it just tries to play a
    plain sine wave for us. First function is =tuner_note= and
    convert note data to sample. Second function is =tuner_fx= which adds
    additional processing to the sound (none in our case).

    Let's load instrument library:

#+NAME: load-instruments
#+BEGIN_SRC extempore
  ;; <load-instruments>
  (sys:load "libs/core/instruments.xtm")
  ;; </load-instruments>
#+END_SRC

    And define helpers for generating sine wave:

#+NAME: define-sine
#+BEGIN_SRC extempore
  ;; <define-sine>
  (bind-val omega SAMPLE (/ STWOPI SRf))

  (bind-func sine
    (lambda (time:i64 freq:SAMPLE)
      (sin (* omega freq (convert time)))))
  ;; </define-sine>
#+END_SRC

    Alternatively, you can use Extempore's built-in =osc_c= generator which
    closes over phase by itself and don't require passing down the time.

    =tuner_note= would be a quite straightforward, very similar to =dsp=
    function from previous chapter, but wrapped in several lambdas to provide
    initialization and context for several layers: instrument instance, note
    instance and calculating note's samples.

#+NAME: tuner-note
#+BEGIN_SRC extempore
  ;; <tuner-note>
  (bind-func tuner_note
    (lambda ()
      ;; here you can put init of entire instrument
      (lambda (data:NoteData* nargs:i64 dargs:SAMPLE*)
        ;; here init of certain note
        (let ((frequency (note_frequency data))
              (amplitude (note_amplitude data))
              (starttime (note_starttime data))
              (duration (note_duration data)))
          (lambda (time:i64 chan:i64)
            ;; here we produce samples for this note
            (if (< (- time starttime) duration)
                (* amplitude (sine time frequency))
                0.0))))))
  ;; </tuner-note>
#+END_SRC

    =tuner_fx= is even easier, because we just pass =tuner_note= result without
    any change:

#+NAME: tuner-fx
#+BEGIN_SRC extempore
  ;; <tuner-fx>
  (bind-func tuner_fx
    (lambda ()
      ;; here put fx init
      (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
        in)))
  ;; </tuner-fx>
#+END_SRC

    =make-instrument= macro allows to glue it together:

#+NAME: make-tuner
#+BEGIN_SRC extempore
  ;; <make-tuner>
  (make-instrument tuner tuner)
  ;; </make-tuner>
#+END_SRC

    The first =tuner= is the name of our instrument, and the second one is
    function name prefix. Extempore than will glue =tuner_note= and =tuner_fx=
    functions. Beware not to make a typo in function names, because otherwise
    segmentation fault is more than probable. Extempore will warn new that
    functino is not found, but then will say that new instrument is bound anyway
    and then will crash trying to play it.

    Next step is to use our brand new instrument in dsp function:

#+NAME: tuner-dsp
#+BEGIN_SRC extempore
  ;; <tuner-dsp>
  (bind-func dsp:DSP
    (lambda (in time chan dat)
      (tuner in time chan dat)))
  ;; </tuner-dsp>
#+END_SRC

    Okay, instrument is set up, let's play a note finally!

#+NAME: play-note-now
#+BEGIN_SRC extempore
  ;; <play-note-now>
  (play-note (now) tuner 60 90 44100)
  ;; </play-note-now>
#+END_SRC

    Wow! That's magic. Here is complete file for instrument and one note. Sip
    your coffee, we'll move to =play-note= signature explanation and playing harmony then.

#+NAME: setup-tuner
#+BEGIN_SRC extempore :noweb yes
  ;; <setup-tuner>
  <<load-instruments>>
  <<define-sine>>
  <<tuner-note>>
  <<tuner-fx>>
  <<make-tuner>>
  <<tuner-dsp>>
  <<set-dsp>>
  ;; </setup-tuner>
#+END_SRC

#+NAME: xtm/03-harmony-tuner.xtm
#+BEGIN_SRC extempore :tangle xtm/03-harmony-tuner.xtm :noweb yes :mkdirp yes :padline no
  ;; <xtm/03-harmony-tuner.xtm>
  <<setup-tuner>>
  <<play-note-now>>
  ;; </xtm/03-harmony-tuner.xtm>
#+END_SRC

    If you want just play chord from course page then don't wait anymore:

#+NAME: play-pleasant-chord
#+BEGIN_SRC extempore
  ;; <play-pleasant-chord>
  (let ((t (now))
        (dur 22050))
    (play-note t tuner 60 100 dur)
    (play-note (+ t (* 2 dur)) tuner 64 100 dur)
    (play-note (+ t (* 4 dur)) tuner 67 100 dur)
    (let ((t (+ t (* 6 dur))))
      (play-note t tuner 60 100 dur)
      (play-note t tuner 64 100 dur)
      (play-note t tuner 67 100 dur)))
  ;; </play-pleasant-chord>
#+END_SRC

    And not so pleasant one:

#+NAME: play-unpleasant-chord
#+BEGIN_SRC extempore
  ;; <play-unpleasant-chord>
  (let ((t (now))
        (dur 22050))
    (play-note t tuner 61 100 dur)
    (play-note (+ t (* 2 dur)) tuner 67 100 dur)
    (play-note (+ t (* 4 dur)) tuner 75 100 dur)
    (let ((t (+ t (* 6 dur))))
      (play-note t tuner 61 100 dur)
      (play-note t tuner 67 100 dur)
      (play-note t tuner 75 100 dur)))
  ;; </play-unpleasant-chord>
#+END_SRC

    Leveraging basic abstractions:

#+NAME: define-pleasant-chord
#+BEGIN_SRC extempore
  ;; <define-pleasant-chord>
  (define pleasant-chord
    (lambda (pitch)
      (list pitch (+ pitch 4) (+ pitch 7))))
  ;; </define-pleasant-chord>
#+END_SRC

#+NAME: define-unpleasant-chord
#+BEGIN_SRC extempore
  ;; <define-unpleasant-chord>
  (define unpleasant-chord
    (lambda (pitch)
      (list (+ pitch 1) (+ pitch 7) (+ pitch 15))))
  ;; </define-unpleasant-chord>
#+END_SRC

#+NAME: play-chord
#+BEGIN_SRC extempore
  ;; <play-chord>
  (define play-chord
    (lambda (t inst pitches dur)
      (let ((together-time (+ t (* 2 (length pitches) dur))))
        (for-each
         (lambda (i pitch)
           (play-note (+ t (* 2 i dur)) inst pitch 100 dur)
           (play-note together-time inst pitch 100 dur))
         (range (length pitches))
         pitches))))
  (play-chord (now) tuner (pleasant-chord 60) 22050)
  ;; (play-chord (now) tuner (unpleasant-chord 60) 22050)
  ;; </play-chord>
#+END_SRC

    And the source file:

#+NAME: xtm/04-harmony-chord.xtm
#+BEGIN_SRC extempore :tangle xtm/04-harmony-chord.xtm :noweb yes :mkdirp yes :padline no
  ;; <xtm/04-harmony-chord.xtm>
  <<setup-tuner>>
  <<define-pleasant-chord>>
  <<define-unpleasant-chord>>
  <<play-chord>>
  ;; </xtm/04-harmony-chord.xtm>
#+END_SRC

    Now let's go into details what's happening in code above.

    First of all, breakdown of =play-note= signature:

    - time :: when note should be started. Time in Extempore is expressed in
              number of samples rendered from its start. Current time is
              available via =now= function.
    - instrument :: instrument to play note with. Remember second-level closure
                    in =instrument_note=? Instrument argument is required to
                    call it and initialize the note we are going to play.
    - pitch :: frequency of the note expressed in terms of [[https://en.wikipedia.org/wiki/MIDI_tuning_standard][MIDI pitch]], 0-127
    - vol :: amplitude of the note expressed as volume, as per formula: =(/ (exp
             (/ vol 26.222)) 127.0)=, 0-127
    - duration :: duration of note. Duration in Extempore is expressed as a
                  number of samples to be generated. If you are rendering sound at 44100Hz
                  sampling rate, then you need to pass 44100 for a 1 second long
                  note.

    Notice that =play-note= allows us to schedule note start at any time.
    We use it in =play-chord= to play all passed pitches one by one and then to
    play them all again, but simultaneously. We schedule all notes at ones, just
    at differents points in time.

**** DONE MIDI controller
     CLOSED: [2017-03-07 Tue 14:32]

     Let's do the trick and play notes from MIDI controller. The latest
     =midi_input= supports defining MIDI callback in Scheme (not only xtlang),
     it will make stuff easier for us because of no need to switch language
     contexts. Replace *19* with your pitch slider CC.

#+NAME: midi-chords-pitch
#+BEGIN_SRC extempore
  ;; <midi-chords-pitch>
  (define *pitch* 60)
  (define midi-cc
    (lambda (timestamp controller value chan)
      (cond ((= controller 19) (set! *pitch* value))
            (else #f))))
  ;; </midi-chords-pitch>
#+END_SRC

     Now let's control note start and stop. Replace *1* with you button NT.

#+NAME: midi-chords-play-button
#+BEGIN_SRC extempore
  ;; <midi-chords-play-button>
  (define midi-note-on
    (lambda (timestamp pitch volume chan)
      (if (= pitch 1)
          (play-chord (now) tuner (pleasant-chord *pitch*) 22050))))
  ;; </midi-chords-play-button>
#+END_SRC

     As an alternative, if you have MIDI keyboard, you can take pitch directly
     from pressed key:

#+NAME: midi-chords-play-keyboard
#+BEGIN_SRC extempore
  ;; <midi-chords-play-keyboard>
  (define midi-note-on
    (lambda (timestamp pitch volume chan)
      (play-chord (now) tuner (pleasant-chord pitch) 22050)))
  ;; </midi-chords-play-keyboard>
#+END_SRC

     To make it work we need to start listener:

#+NAME: start-midi-listener
#+BEGIN_SRC extempore
  ;; <start-midi-listener>
  (scheme-midi-listener (*metro* 'get-beat 4) 1/24))
  ;; </start-midi-listener>
#+END_SRC

     And whole files for button and keyboard:

#+NAME: xtm/05-midi-chord-button.xtm
#+BEGIN_SRC extempore :tangle xtm/05-midi-chord-button.xtm :noweb yes :mkdirp yes :paddle no
  ;; <xtm/05-midi-chord-button.xtm>
  <<xtm/04-harmony-chord.xtm>>
  <<midi-chords-pitch>>
  <<midi-chords-play-button>>
  <<start-midi-listener>>
  ;; </xtm/05-midi-chord-button.xtm>
#+END_SRC

#+NAME: xtm/06-midi-chord-keyboard.xtm
#+BEGIN_SRC extempore :tangle xtm/06-midi-chord-keyboard.xtm :noweb yes :mkdirp yes :paddle no
  ;; <xtm/06-midi-chord-keyboard.xtm>
  <<xtm/04-harmony-chord.xtm>>
  <<midi-chords-play-keyboard>>
  <<start-midi-listener>>
  ;; </xtm/06-midi-chord-keyboard.xtm>
#+END_SRC

*** Intermezzo: =osc_c=

    I mentioned Extempore's =osc_c= briefly as an alternative for hand-rolled
    sine wave generator. Now it's time to write down (and hear!) difference
    between the two. =osc_c= encloses phase, and our =sine= takes it implicitly
    as a timestamp. But in this case it's not just a question of style (FP-ish
    explicit argument passing vs OOP-y mixing state with code), but a subtle
    difference in behavior. Waves produced by both oscillators are the same when
    frequency stays constant. But =sine= goes glitchy when frequency changes,
    that's why usually =osc_c= is the way to go (though sometimes you want to
    produce glitches on purpose).

    To hear the difference let's apply frequency modulation to our =tuner= and
    make... hmmm... =fm_tuner= instrument ;-)

#+NAME: fm-tuner-note
#+BEGIN_SRC extempore
  ;; <fm-tuner-note>
  (bind-func fm_tuner_note
    (lambda ()
      ;; here you can put init of entire instrument
      (lambda (data:NoteData* nargs:i64 dargs:SAMPLE*)
        ;; here init of certain note
        (let ((frequency (note_frequency data))
              (amplitude (note_amplitude data))
              (starttime (note_starttime data))
              (duration (note_duration data)))
          (lambda (time:i64 chan:i64)
            ;; here we produce samples for this note
            (if (< (- time starttime) duration)
                (* amplitude
                   (sine time (+ frequency
                                 (* 50.0
                                    (sine time (* 0.1 frequency))))))
                0.0))))))
  ;; </fm-tuner-note>
#+END_SRC

    And =fm_tuner_fx= will still do nothing (but don't hesitate to edit it by
    your taste!)

#+NAME: fm-tuner-fx
#+BEGIN_SRC extempore
  ;; <fm-tuner-fx>
  (bind-func fm_tuner_fx
    (lambda ()
      (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
        in)))
  ;; </fm-tuner-fx>
#+END_SRC

    The moment of truth, our poor-man FM-synth sound:

#+NAME: xtm/07-fm-tuner-sine.xtm
#+BEGIN_SRC extempore :tangle xtm/07-fm-tuner-sine.xtm :noweb yes :mkdirp yes :paddle no
  ;; <xtm/07-fm-tuner-sine.xtm>
  <<load-instruments>>
  <<define-sine>>
  <<fm-tuner-note>>
  <<fm-tuner-fx>>

  (make-instrument fm_tuner fm_tuner)

  (bind-func dsp:DSP
    (lambda (in time chan dat)
      (fm_tuner in time chan dat)))

  <<set-dsp>>

  (play-note (now) fm_tuner 60 90 44100)
  ;; </xtm/07-fm-tuner-sine.xtm>
#+END_SRC

   Do you hear? It's not even glitchy, it's just a noise. Let's do the same
   synth using =osc_c=:

#+NAME: fm-tuner-note-osc
#+BEGIN_SRC extempore
  ;; <fm-tuner-note-osc>
  (bind-func fm_tuner_note
    (lambda ()
      ;; here you can put init of entire instrument
      (lambda (data:NoteData* nargs:i64 dargs:SAMPLE*)
        ;; here init of certain note
        (let ((frequency (note_frequency data))
              (amplitude (note_amplitude data))
              (starttime (note_starttime data))
              (duration (note_duration data))
              (carrier (osc_c 0.0))
              (modulator (osc_c 0.0)))
          (lambda (time:i64 chan:i64)
            ;; here we produce samples for this note
            (if (< (- time starttime) duration)
                (carrier amplitude
                         (+ frequency
                            (modulator 50.0 (* 0.1 frequency))))
                0.0))))))
  ;; </fm-tuner-note-osc>
#+END_SRC

   And the file:

#+NAME: xtm/08-fm-tuner-osc.xtm
#+BEGIN_SRC extempore :tangle xtm/08-fm-tuner-osc.xtm :noweb yes :mkdirp yes :paddle no
  ;; <xtm/08-fm-tuner-osc.xtm>
  <<load-instruments>>
  <<fm-tuner-note-osc>>
  <<fm-tuner-fx>>

  (make-instrument fm_tuner fm_tuner)

  (bind-func dsp:DSP
    (lambda (in time chan dat)
      (fm_tuner in time chan dat)))

  <<set-dsp>>

  (play-note (now) fm_tuner 60 90 44100)
  ;; </xtm/08-fm-tuner-osc.xtm>
#+END_SRC

  This one is so nice, isn't it? Viva la =osc_c= ;-)
