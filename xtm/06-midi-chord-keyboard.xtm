
;; <xtm/06-midi-chord-keyboard.xtm>
;; <xtm/04-harmony-chord.xtm>
;; <setup-tuner>
;; <load-instruments>
(sys:load "libs/core/instruments.xtm")
;; </load-instruments>
;; <define-sine>
(bind-val omega SAMPLE (/ STWOPI SRf))

(bind-func sine
  (lambda (time:i64 freq:SAMPLE)
    (sin (* omega freq (convert time)))))
;; </define-sine>
;; <tuner-note>
(bind-func tuner_note
  (lambda ()
    ;; here you can put init of entire instrument
    (lambda (data:NoteData* nargs:i64 dargs:SAMPLE*)
      ;; here init of certain note
      (let ((frequency (note_frequency data))
            (amplitude (note_amplitude data))
            (starttime (note_starttime data))
            (duration (note_duration data)))
        (lambda (time:i64 chan:i64)
          ;; here we produce samples for this note
          (if (< (- time starttime) duration)
              (* amplitude (sine time frequency))
              0.0))))))
;; </tuner-note>
;; <tuner-fx>
(bind-func tuner_fx
  (lambda ()
    ;; here put fx init
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      in)))
;; </tuner-fx>
;; <make-tuner>
(make-instrument tuner tuner)
;; </make-tuner>
;; <tuner-dsp>
(bind-func dsp:DSP
  (lambda (in time chan dat)
    (tuner in time chan dat)))
;; </tuner-dsp>
;; <set-dsp>
(dsp:set! dsp)
;; </set-dsp>
;; </setup-tuner>
;; <define-pleasant-chord>
(define pleasant-chord
  (lambda (pitch)
    (list pitch (+ pitch 4) (+ pitch 7))))
;; </define-pleasant-chord>
;; <define-unpleasant-chord>
(define unpleasant-chord
  (lambda (pitch)
    (list (+ pitch 1) (+ pitch 7) (+ pitch 15))))
;; </define-unpleasant-chord>
;; <play-chord>
(define play-chord
  (lambda (t inst pitches dur)
    (let ((together-time (+ t (* 2 (length pitches) dur))))
      (for-each
       (lambda (i pitch)
         (play-note (+ t (* 2 i dur)) inst pitch 100 dur)
         (play-note together-time inst pitch 100 dur))
       (range (length pitches))
       pitches))))
;; </play-chord>
(play-chord (now) tuner (pleasant-chord 60) 22050)
;; (play-chord (now) tuner (unpleasant-chord 60) 22050)
;; </xtm/04-harmony-chord.xtm>
;; <midi-chords-play-keyboard>
(define midi-note-on
  (lambda (timestamp pitch volume chan)
    (play-chord (now) tuner (pleasant-chord pitch) 22050)))
;; </midi-chords-play-keyboard>
;; <start-midi-listener>
(scheme-midi-listener (*metro* 'get-beat 4) 1/24))
;; </start-midi-listener>
;; </xtm/06-midi-chord-keyboard.xtm>
